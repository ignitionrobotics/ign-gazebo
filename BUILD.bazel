load(
    "//ign_bazel:build_defs.bzl",
    "IGNITION_ROOT",
    "IGNITION_VISIBILITY",
    "cmake_configure_file",
    "generate_include_header",
    "generate_yaml",
    "ign_config_header",
    "ign_export_header",
)
load(
    "//ign_bazel:qt.bzl",
    "qt_cc_binary",
    "qt_cc_library",
)
load(
    "//ign_msgs:ign_msg_gen.bzl",
    "get_proto_headers",
    "ign_msg_gen",
)

package(
    default_visibility = IGNITION_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

licenses(["notice"])

exports_files(["LICENSE"])

PROJECT_NAME = "ignition-gazebo"

PROJECT_MAJOR = 4

PROJECT_MINOR = 0

PROJECT_PATCH = 0

# Generates config.hh based on the version numbers in CMake code.
ign_config_header(
    name = "config",
    src = "include/ignition/gazebo/config.hh.in",
    out = "include/ignition/gazebo/config.hh",
    cmakelists = ["CMakeLists.txt"],
    extra_defines = [
        "CMAKE_INSTALL_PREFIX=./ign_gazebo/",
        "IGN_DATA_INSTALL_DIR=./ign_gazebo/",
        "IGN_LIB_INSTALL_DIR=./ign_gazebo/",
    ],
    project_name = PROJECT_NAME,
    project_version = (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
)

ign_export_header(
    name = "include/ignition/gazebo/Export.hh",
    export_base = "IGNITION_GAZEBO",
    lib_name = "ignition-gazebo",
    visibility = ["//visibility:private"],
)

public_component_headers_no_gen = glob(
    ["include/ignition/gazebo/components/*.hh"],
)

public_headers_no_gen = glob([
    "include/ignition/gazebo/*.hh",
    "include/ignition/gazebo/detail/*.hh",
    "include/ignition/gazebo/gui/*.hh",
    "include/ignition/gazebo/rendering/*.hh",
])

private_headers = [
    "src/Barrier.hh",
    "src/ign.hh",
    "src/LevelManager.hh",
    "src/SdfGenerator.hh",
    "src/ServerPrivate.hh",
    "src/SimulationRunner.hh",
    "src/network/NetworkConfig.hh",
    "src/network/NetworkManager.hh",
    "src/network/NetworkManagerPrimary.hh",
    "src/network/NetworkManagerSecondary.hh",
    "src/network/NetworkManagerPrivate.hh",
    "src/network/NetworkRole.hh",
    "src/network/PeerInfo.hh",
    "src/network/PeerTracker.hh",
]

sources = [
    "src/Barrier.cc",
    "src/Conversions.cc",
    "src/EntityComponentManager.cc",
    "src/EventManager.cc",
    "src/LevelManager.cc",
    "src/Link.cc",
    "src/Model.cc",
    "src/SdfEntityCreator.cc",
    "src/SdfGenerator.cc",
    "src/Server.cc",
    "src/ServerConfig.cc",
    "src/ServerPrivate.cc",
    "src/SimulationRunner.cc",
    "src/System.cc",
    "src/SystemLoader.cc",
    "src/Util.cc",
    "src/View.cc",
    "src/rendering/MarkerManager.cc",
    "src/rendering/RenderUtil.cc",
    "src/rendering/SceneManager.cc",
    "src/network/NetworkConfig.cc",
    "src/network/NetworkManager.cc",
    "src/network/NetworkManagerPrimary.cc",
    "src/network/NetworkManagerSecondary.cc",
    "src/network/PeerInfo.cc",
    "src/network/PeerTracker.cc",
]

test_sources = [
    "src/Barrier_TEST.cc",
    "src/ComponentFactory_TEST.cc",
    "src/Component_TEST.cc",
    "src/Conversions_TEST.cc",
    "src/EntityComponentManager_TEST.cc",
    "src/EventManager_TEST.cc",
    "src/Link_TEST.cc",
    "src/Model_TEST.cc",
    "src/SdfEntityCreator_TEST.cc",
    # "src/SdfGenerator_TEST.cc",
    # "src/Server_TEST.cc",
    "src/SimulationRunner_TEST.cc",
    "src/SystemLoader_TEST.cc",
    "src/System_TEST.cc",
    "src/Util_TEST.cc",
]

systems = [
    "air_pressure",
    "altimeter",
    "apply_joint_force",
    "battery_plugin",
    "breadcrumbs",
    "buoyancy",
    "camera_video_recorder",
    "contact",
    "detachable_joint",
    "diff_drive",
    "follow_actor",
    "imu",
    "joint_controller",
    "joint_position_controller",
    "joint_state_publisher",
    "lift_drag",
    "log",
    "logical_camera",
    "log_video_recorder",
    "magnetometer",
    "multicopter_control",
    "multicopter_motor_model",
    "performer_detector",
    "physics",
    "pose_publisher",
    "scene_broadcaster",
    "sensors",
    "touch_plugin",
    "triggered_publisher",
    "user_commands",
    "velocity_control",
    "wheel_slip",
    "wind_effects",
]

protos = glob(
    ["src/msgs/*.proto"],
    exclude = ["src/msgs/performer_affinity.proto"],
)

proto_library(
    name = "performer_affinity_proto",
    srcs = ["src/msgs/performer_affinity.proto"],
    strip_import_prefix = "src/msgs",
    deps = [
        "//ign_msgs:ignmsgs_proto_public",
    ],
)

proto_library(
    name = "igngazebo_proto",
    srcs = protos,
    strip_import_prefix = "src",
    deps = [
        ":performer_affinity_proto",
        "//ign_msgs:ignmsgs_proto_public",
    ],
)

cc_proto_library(
    name = "igngazebo_proto_cc",
    deps = [
        ":igngazebo_proto",
    ],
)

generate_include_header(
    name = "gazebohh_genrule",
    out = "include/ignition/gazebo.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/gazebo/config.hh",
        "include/ignition/gazebo/Export.hh",
    ],
)

generate_include_header(
    name = "gazebo_componentshh_genrule",
    out = "include/ignition/gazebo/components/components.hh",
    hdrs = public_component_headers_no_gen,
)

public_headers = public_headers_no_gen + public_component_headers_no_gen + [
    "include/ignition/gazebo/config.hh",
    "include/ignition/gazebo/Export.hh",
    "include/ignition/gazebo.hh",
    "include/ignition/gazebo/components/components.hh",
]

cc_library(
    name = "ign_gazebo_headers",
    hdrs = private_headers + public_headers,
    includes = ["include"],
)

genrule(
    name = "resources_gen",
    srcs = [
        "src/gui/resources/gazebo.qrc",
        "src/gui/resources/GazeboDrawer.qml",
    ] + glob(["src/gui/resources/images/*"]),
    outs = [
        "qrc_gazebo.cpp",
    ],
    cmd = "cd ign_gazebo/src/gui/resources/ && qtchooser -qt=5 -run-tool=rcc --name gazebo gazebo.qrc -o qrc_gazebo.cpp && cp qrc_gazebo.cpp ../../../../$(OUTS)",
)

filegroup(
    name = "example-worlds",
    srcs = glob([
        "examples/worlds/*.sdf",
    ]),
)

qt_cc_library(
    name = "ign_gazebo_gui",
    srcs = ["qrc_gazebo.cpp"] + glob(
        ["src/gui/*.cc"],
        exclude = ["src/gui/*_TEST.cc"],
    ),
    hdrs = [
        "include/ignition/gazebo/gui/Gui.hh",
        "include/ignition/gazebo/gui/GuiEvents.hh",
        "include/ignition/gazebo/gui/GuiRunner.hh",
        "include/ignition/gazebo/gui/GuiSystem.hh",
        "include/ignition/gazebo/gui/TmpIface.hh",
    ] + glob(["src/gui/*.hh"]),
    includes = [
        "include",
        "src",
    ],
    linkopts = [],
    deps = [
        ":ign_gazebo",
        ":ign_gazebo_headers",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_common/profiler",
        IGNITION_ROOT + "ign_fuel_tools",
        IGNITION_ROOT + "ign_gui",
        IGNITION_ROOT + "ign_msgs",
        IGNITION_ROOT + "ign_transport",
        IGNITION_ROOT + "sdformat",
    ],
)

cc_library(
    name = "ign_gazebo",
    srcs = sources + private_headers,
    hdrs = public_headers,
    includes = ["include"],
    deps = [
        ":igngazebo_proto_cc",
        IGNITION_ROOT + "ign_bazel:utilities",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_common/events",
        IGNITION_ROOT + "ign_common/graphics",
        IGNITION_ROOT + "ign_common/profiler",
        IGNITION_ROOT + "ign_fuel_tools",
        IGNITION_ROOT + "ign_gui",
        IGNITION_ROOT + "ign_msgs",
        IGNITION_ROOT + "ign_plugin",
        IGNITION_ROOT + "ign_plugin/loader",
        IGNITION_ROOT + "ign_rendering",
        IGNITION_ROOT + "ign_transport",
        IGNITION_ROOT + "sdformat",
    ],
)

cc_binary(
    name = "libignition-gazebo.so",
    srcs = ["src/ign.cc"],
    linkshared = True,
    deps = [
        ":ign_gazebo",
        ":ign_gazebo_gui",
    ],
)

[
    cc_binary(
        name = "libignition-gazebo-%s-system.so" % system.replace("_", "-"),
        srcs = glob(
            [
                "src/systems/%s/*.cc" % system,
                "src/systems/%s/*.hh" % system,
            ],
            allow_empty = False,
        ),
        includes = ["include"],
        linkshared = True,
        visibility = ["//visibility:public"],
        deps = [
            IGNITION_ROOT + "ign_bazel:utilities",
            IGNITION_ROOT + "ign_common",
            IGNITION_ROOT + "ign_common/av",
            IGNITION_ROOT + "ign_common/events",
            IGNITION_ROOT + "ign_common/graphics",
            IGNITION_ROOT + "ign_common/profiler",
            IGNITION_ROOT + "ign_fuel_tools",
            IGNITION_ROOT + "ign_gazebo",
            IGNITION_ROOT + "ign_gui",
            IGNITION_ROOT + "ign_math/eigen3",
            IGNITION_ROOT + "ign_msgs",
            IGNITION_ROOT + "ign_physics",
            IGNITION_ROOT + "ign_physics:mesh",
            IGNITION_ROOT + "ign_physics:sdf",
            IGNITION_ROOT + "ign_physics/dartsim:libignition-physics-dartsim-plugin.so",
            IGNITION_ROOT + "ign_plugin",
            IGNITION_ROOT + "ign_plugin/loader",
            IGNITION_ROOT + "ign_plugin/register",
            IGNITION_ROOT + "ign_rendering",
            IGNITION_ROOT + "ign_sensors",
            IGNITION_ROOT + "ign_transport",
            IGNITION_ROOT + "ign_transport/log:ign_transport-log",
            IGNITION_ROOT + "sdformat",
        ],
    )
    for system in systems
]

[
    cc_library(
        name = system,
        srcs = [
            "libignition-gazebo-%s-system.so" % system.replace("_", "-"),
        ],
        hdrs = glob(
            [
                "src/systems/%s/*.hh" % system,
            ],
            allow_empty = False,
        ),
        deps = [
            IGNITION_ROOT + "ign_bazel:utilities",
            IGNITION_ROOT + "ign_common",
            IGNITION_ROOT + "ign_common/events",
            IGNITION_ROOT + "ign_common/graphics",
            IGNITION_ROOT + "ign_common/profiler",
            IGNITION_ROOT + "ign_fuel_tools",
            IGNITION_ROOT + "ign_gazebo",
            IGNITION_ROOT + "ign_gui",
            IGNITION_ROOT + "ign_math/eigen3",
            IGNITION_ROOT + "ign_msgs",
            IGNITION_ROOT + "ign_physics",
            IGNITION_ROOT + "ign_physics:mesh",
            IGNITION_ROOT + "ign_physics:sdf",
            IGNITION_ROOT + "ign_plugin",
            IGNITION_ROOT + "ign_plugin/register",
            IGNITION_ROOT + "ign_rendering",
            IGNITION_ROOT + "ign_sensors",
            IGNITION_ROOT + "ign_transport",
            IGNITION_ROOT + "ign_transport/log:ign_transport-log",
            IGNITION_ROOT + "sdformat",
        ],
    )
    for system in systems
]

cc_binary(
    name = "libignition-gazebo-thermal-system.so",
    srcs = [
        "src/systems/thermal/Thermal.cc",
        "src/systems/thermal/Thermal.hh",
    ],
    includes = ["include"],
    linkshared = True,
    deps = [
        IGNITION_ROOT + "ign_gazebo",
        IGNITION_ROOT + "ign_plugin",
        IGNITION_ROOT + "ign_plugin/loader",
        IGNITION_ROOT + "ign_plugin/register",
        IGNITION_ROOT + "ign_rendering",
        IGNITION_ROOT + "ign_sensors",
        IGNITION_ROOT + "ign_transport",
        IGNITION_ROOT + "ign_transport/log:ign_transport-log",
        IGNITION_ROOT + "sdformat",
    ],
)

cc_binary(
    name = "libignition-gazebo-thermal-sensor-system.so",
    srcs = [
        "src/systems/thermal/ThermalSensor.cc",
        "src/systems/thermal/ThermalSensor.hh",
    ],
    includes = ["include"],
    linkshared = True,
    deps = [
        IGNITION_ROOT + "ign_gazebo",
        IGNITION_ROOT + "ign_plugin",
        IGNITION_ROOT + "ign_plugin/loader",
        IGNITION_ROOT + "ign_plugin/register",
        IGNITION_ROOT + "ign_rendering",
        IGNITION_ROOT + "ign_sensors",
        IGNITION_ROOT + "ign_transport",
        IGNITION_ROOT + "ign_transport/log:ign_transport-log",
        IGNITION_ROOT + "sdformat",
    ],
)

cc_library(
    name = "ign_gazebo_systems",
    deps = [
        IGNITION_ROOT + "ign_gui/src/plugins",
        IGNITION_ROOT + "ign_gazebo/src/gui/plugins:libGzScene3D.so",
        IGNITION_ROOT + "ign_gazebo/src/gui/plugins:libComponentInspector.so",
        IGNITION_ROOT + "ign_gazebo/src/gui/plugins:libEntityTree.so",
        IGNITION_ROOT + "ign_gazebo/src/gui/plugins:libTransformControl.so",
        IGNITION_ROOT + "ign_gazebo/src/gui/plugins:libShapes.so",
        IGNITION_ROOT + "ign_gazebo/src/gui/plugins:IgnGazebo/libEntityContextMenu.so",
        ":libignition-gazebo-thermal-system.so",
        ":libignition-gazebo-thermal-sensor-system.so",
    ] + [":libignition-gazebo-%s-system.so" % system.replace("_", "-") for system in systems],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src] + private_headers,
    deps = [
        ":ign_gazebo_systems",
        IGNITION_ROOT + "ign_bazel:utilities",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_common/events",
        IGNITION_ROOT + "ign_common/graphics",
        IGNITION_ROOT + "ign_common/profiler",
        IGNITION_ROOT + "ign_fuel_tools",
        IGNITION_ROOT + "ign_gazebo",
        IGNITION_ROOT + "ign_gazebo/test:test_systems",
        IGNITION_ROOT + "ign_gazebo/test:test_utils",
        IGNITION_ROOT + "ign_gui",
        IGNITION_ROOT + "ign_math/eigen3",
        IGNITION_ROOT + "ign_msgs",
        IGNITION_ROOT + "ign_physics",
        IGNITION_ROOT + "ign_physics:mesh",
        IGNITION_ROOT + "ign_physics:sdf",
        IGNITION_ROOT + "ign_plugin",
        IGNITION_ROOT + "ign_plugin/loader",
        IGNITION_ROOT + "ign_plugin/register",
        IGNITION_ROOT + "ign_rendering",
        IGNITION_ROOT + "ign_sensors",
        IGNITION_ROOT + "ign_transport",
        IGNITION_ROOT + "ign_transport/log:ign_transport-log",
        IGNITION_ROOT + "sdformat",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]

cmake_configure_file(
    name = "gazebo.rb",
    src = "src/cmd/cmdgazebo.rb.in",
    out = "cmdgazebo.rb",
    cmakelists = ["CMakeLists.txt"],
    defines = [
        "library_location=libignition-gazebo.so",
        "PROJECT_VERSION_FULL=%d.%d.%d" % (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),  # noqa
        "IGN_LIBRARY_NAME=%s" % PROJECT_NAME,
    ],
)

CMDS = "    - gazebo   : Run and manage Gazebo."

generate_yaml(
    name = "gazebo",
    commands = CMDS,
    library_name = PROJECT_NAME,
    library_version = "%d.%d.%d" % (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
    ruby_target = "gazebo.rb",
)
